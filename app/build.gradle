apply plugin: 'com.android.application'

apply plugin: 'kotlin-android'

apply plugin: 'kotlin-android-extensions'

android {
    def configuration = rootProject.extensions.getByName("ext")
    compileSdkVersion configuration.androidCompileSdkVersion
    defaultConfig {
        applicationId "com.wx.rocky.frameforandroid"
        minSdkVersion configuration.androidMinSdkVersion
        targetSdkVersion configuration.androidTargetSdkVersion
        versionCode 1
        versionName "1.0"
        multiDexEnabled true
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    lintOptions {
        abortOnError false
    }

    signingConfigs {
        myConfig {
            storeFile file(RELEASE_STORE_FILE)
            storePassword RELEASE_STORE_PASSWORD
            keyAlias RELEASE_KEY_ALIAS
            keyPassword RELEASE_KEY_PASSWORD
        }
    }

    buildTypes {

        debug {
            debuggable true
            minifyEnabled false
            shrinkResources false
            zipAlignEnabled false
            signingConfig signingConfigs.myConfig
            buildConfigField "int", "APP_DEBUG", "0"
        }

        release {
            debuggable true
            minifyEnabled true
            shrinkResources true
            zipAlignEnabled true
            signingConfig signingConfigs.myConfig
            buildConfigField "int", "APP_DEBUG", "1"
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            //关键代码，release， debug为library中已有buildType
            // 如果flavor或buildType存在不一致时，是否添加了matchingFallbacks指定匹配失败时匹配其他变体
            matchingFallbacks = ['release', 'debug']
        }
    }

    android.applicationVariants.all { variant ->
        variant.outputs.all {
            outputFileName = "ODR2-${variant.versionCode}-${buildTime()}-${variant.buildType.name}.apk".toLowerCase()
        }
    }
}

def buildTime() {
    return new Date().format("yyyy-MM-dd", TimeZone.getTimeZone("UTC"))
}

task copyOutputApk(type: Copy) {
    from 'build/outputs/apk'
    into 'apk'
    exclude '**/*-unaligned.apk'
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation"org.jetbrains.kotlin:kotlin-stdlib-jre7:$kotlin_version"
    implementation 'com.android.support:appcompat-v7:28.0.0'
    implementation 'com.android.support.constraint:constraint-layout:1.1.3'
    implementation 'com.android.support:design:28.0.0'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
}
